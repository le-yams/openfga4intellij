
{

  parserClass="com.github.le_yams.openfga4intellij.parsing.OpenFGAParser"
  parserUtilClass="com.github.le_yams.openfga4intellij.parsing.OpenFGAParserUtil"

  psiClassPrefix="OpenFGA"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.le_yams.openfga4intellij.psi"
  psiImplPackage="com.github.le_yams.openfga4intellij.psi.impl"

  elementTypeHolderClass="com.github.le_yams.openfga4intellij.psi.OpenFGATypes"
  elementTypeClass="com.github.le_yams.openfga4intellij.parsing.OpenFGAElementType"
  tokenTypeClass="com.github.le_yams.openfga4intellij.parsing.OpenFGATokenType"

}

openfgaFile ::= main
main ::= headerMultiLineComment? newline? ( modelHeader | moduleHeader ) typeDefs newline? conditionDefs
moduleHeader ::= MODULE moduleName
modelHeader ::= MODEL multiLineComment? indentedLine1 SCHEMA SCHEMA_VERSION
typeDefs ::= typeDef*
typeDef ::= multiLineComment? newline (EXTEND)? TYPE typeIdentifier (indentedLine1 RELATIONS relationDeclaration+)?
relationDeclaration ::= multiLineComment? indentedLine2 DEFINE relationName COLON relationDef

relationDef ::= (relationDefDirectAssignment | relationDefGrouping | relationRecurse) relationDefPartials?
relationDefNoDirect ::= (relationDefGrouping | relationRecurseNoDirect) (relationDefPartials)?;
relationDefPartials ::=
    (OR (relationDefGrouping | relationRecurseNoDirect))+
    | (AND (relationDefGrouping | relationRecurseNoDirect))+
    | (BUT_NOT (relationDefGrouping | relationRecurseNoDirect));
relationRecurse ::= L_PAREN (relationDef | relationRecurseNoDirect) R_PAREN;
relationRecurseNoDirect ::= L_PAREN (relationDefNoDirect | relationRecurseNoDirect) R_PAREN;
relationDefDirectAssignment ::= L_SQUARE relationDefTypeRestriction (COMMA relationDefTypeRestriction)* R_SQUARE

relationDefTypeRestrictionBase ::= relationDefTypeRestrictionWildcard | relationDefTypeRestrictionUserset | relationDefTypeRestrictionRelation
relationDefTypeRestriction ::= relationDefTypeRestrictionBase (WITH conditionNameRef)?

relationDefRewrite ::= relationDefRelationOnRelatedObject | relationDefRelationOnSameObject
relationDefRelationOnSameObject ::= rewriteComputedusersetName
relationDefRelationOnRelatedObject ::= rewriteTuplesetComputedusersetName FROM rewriteTuplesetName
relationDefTypeRestrictionRelation ::= typeReference
relationDefTypeRestrictionWildcard ::= typeReference COLON WILDCARD
relationDefTypeRestrictionUserset ::= typeReference HASH relationDefTypeRestrictionRelation
relationDefGrouping ::= relationDefRewrite
rewriteComputedusersetName ::= identifier
rewriteTuplesetComputedusersetName ::= identifier
rewriteTuplesetName ::= identifier
moduleName ::= identifier
relationName ::= identifier
typeIdentifier ::= identifier
typeReference ::= identifier

// Conditions
// https://github.com/google/cel-spec/blob/f9ce1ea675260361a667d29b79ee15fd9768028a/doc/langdef.md#syntax
conditionDefs ::= conditions*
conditions ::= (
    (headerMultiLineComment newline)?
    CONDITION conditionName
    L_PAREN conditionParameter (COMMA conditionParameter)* newline? R_PAREN
    CONDITION_EXPRESSION
    ) newline?
conditionName ::= identifier
conditionNameRef ::= identifier
conditionParameter ::= newline? parameterName COLON parameterType
parameterName ::= identifier
parameterType ::= parameterTypeNames | (("map"|"list") LESS parameterTypeNames GREATER)
parameterTypeNames ::= ("bool"|"string"|"int"|"uint"|"double"|"duration"|"timestamp"|"ipaddress")

// Simple tokens
identifier ::= ALPHA_NUMERIC | MODEL | SCHEMA | TYPE | RELATION | MODULE | EXTEND
indentedLine1 ::= newline IDENT1
indentedLine2 ::= newline IDENT2
newline ::= END_OF_LINE+
comment ::= SINGLE_LINE_COMMENT
headerMultiLineComment ::= comment (newline comment)*
multiLineComment ::= newline comment (newline comment)*